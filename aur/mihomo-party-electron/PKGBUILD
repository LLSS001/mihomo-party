pkgname=clash-meta-party-electron
_pkgname=clash-meta-party
pkgver=0.1.3
pkgrel=1
pkgdesc="Another Mihomo GUI."
arch=('x86_64' 'aarch64')
url="https://github.com/clash-meta-party-org/clash-meta-party"
license=('GPL3')
conflicts=("$_pkgname" "$_pkgname-git" "$_pkgname-bin" "$_pkgname-electron-bin")
depends=('electron' 'gtk3' 'libnotify' 'nss' 'libxss' 'libxtst' 'xdg-utils' 'at-spi2-core' 'util-linux-libs' 'libsecret')
optdepends=('libappindicator-gtk3: Allow clash-meta-party to extend a menu via Ayatana indicators in Unity, KDE or Systray (GTK+ 3 library).')
makedepends=('nodejs' 'pnpm' 'libxcrypt-compat' 'asar')
install=$_pkgname.install
source=(
    "${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
    "${_pkgname}.desktop"
    "${_pkgname}.sh"
)
sha256sums=("52d761e9432e17477acb8adb5744676df946476e0eb5210fee2b6d45f497f218"
"96a6250f67517493f839f964c024434dbcf784b25a73f074bb505f1521f52844"
"560733f0e5bd9b47ff50c849301c8a22ae17a5df26830d8c97033dfcbd392382"
)
options=('!lto')

prepare(){
    cd $srcdir/${_pkgname}-${pkgver}
    sed -i "s/productName: Mihomo Party/productName: clash-meta-party/" electron-builder.yml
    pnpm install
}

build(){
    cd $srcdir/${_pkgname}-${pkgver}
    pnpm build:linux deb
}

package() {
    asar extract $srcdir/${_pkgname}-${pkgver}/dist/linux-unpacked/resources/app.asar ${pkgdir}/opt/clash-meta-party
    cp -r $srcdir/${_pkgname}-${pkgver}/extra/sidecar ${pkgdir}/opt/clash-meta-party/resources/
    cp -r $srcdir/${_pkgname}-${pkgver}/extra/files ${pkgdir}/opt/clash-meta-party/resources/
    chmod +sx ${pkgdir}/opt/clash-meta-party/resources/sidecar/clash-meta
    chmod +sx ${pkgdir}/opt/clash-meta-party/resources/sidecar/clash-meta-alpha
    install -Dm755 "${_pkgname}.sh" "${pkgdir}/usr/bin/${_pkgname}"
    install -Dm644 "${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
    install -Dm644 "${pkgdir}/opt/clash-meta-party/resources/icon.png" "${pkgdir}/usr/share/icons/hicolor/512x512/apps/${_pkgname}.png"

    chown -R root:root ${pkgdir}
}
